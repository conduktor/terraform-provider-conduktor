---
page_title: "Conduktor : conduktor_generic "
description: |-
    Generic Resource that use manifests in YAML format.
    This resource allows you to create, read, update and delete any resource supported by Conduktor Console.
---

# conduktor_generic

Generic Resource that use manifests in YAML format.
This resource allows you to create, read, update and delete any resource supported by Conduktor Console.

> **Caution**
>
> This resource is experimental and have many [limitations](#limitations) and might have breaking changes in next releases.
> Be aware that using it to manage production environement is not recommended as it will cause [migrations](./migrations-notes) issues in future.

## Example Usage

### Using `yamlencoded` HCL value
```terraform
resource "conduktor_generic" "embedded" {
  kind    = "User"
  version = "v2"
  name    = "martin@company.io"
  manifest = yamlencode({
    apiVersion = "v2"
    kind       = "User"
    metadata = {
      name = "martin@company.io"
    }
    spec = {
      firstName = "Martin"
      lastName  = "Smith"
      permissions = [
        {
          permissions = [
            "userView",
            "datamaskingView",
            "auditLogView"
          ]
          resourceType = "PLATFORM"
        },
        {
          permissions = [
            "topicViewConfig",
            "topicConsume",
            "topicProduce"
          ]
          resourceType = "TOPIC"
          name         = "test-topic"
          cluster      = "*"
          patternType  = "LITERAL"
        }
      ]
    }
  })
}
```

### Using normalized raw yaml value
```terraform
resource "conduktor_generic" "raw_yaml" {
  kind    = "User"
  version = "v2"
  name    = "alice@company.io"
  manifest = yamlencode(yamldecode(<<EOF
      apiVersion: v2
      kind: User
      metadata:
        name: "alice@company.io"
      spec:
        firstName: "Alice"
        lastName: "Smith"
        permissions:
          - resourceType: PLATFORM
            permissions: ["userView", "datamaskingView", "auditLogView"]
          - resourceType: TOPIC
            cluster: '*'
            name: test-topic
            patternType: LITERAL
            permissions: [ "topicViewConfig", "topicConsume", "topicProduce" ]
      EOF
  ))
}
```

### Using included YAML string
```terraform
resource "conduktor_generic" "test" {
  kind     = "KafkaCluster"
  version  = "v2"
  name     = "cluserA"
  manifest = file("clusterA.yaml")
}
```
```yaml
#cluster_a.yaml
apiVersion: v2
kind: KafkaCluster
metadata:
  name: cluster_a
spec:
  displayName: "Cluster A"
  bootstrapServers: "localhost:9092"
  ignoreUntrustedCertificate: false
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `kind` (String) Resource kind
- `manifest` (String) Resource manifest in yaml format. Use `yamlencode`/`yamldecode` function to normalize input and avoid dirty plan. See [reference documentation](https://docs.conduktor.io/platform/reference/resource-reference/console/#manifests) for more details
- `name` (String) Resource name
- `version` (String) Resource version

### Optional

- `cluster` (String) Resource parent cluster (if any)




## Limitations

Current limitation of this resource are :

- `import` is not supported.
- only support Console resources for now

## Migrations notes

In future releases, this terraform provider will support more and more typed resources and you might end up migrating a resource from this generic resource to the typed one.

This migration can only be done by destroying previous resource on Conduktor and recreate it after using the new typed resouce.

Because of that you will need to be extra careful of the current state of the resource before doing migrations.
